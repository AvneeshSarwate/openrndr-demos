
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

ext.openrndrVersion = "0.3.39"
ext.orxVersion = "0.3.48"

repositories {
    mavenCentral()
    maven { url='https://dl.bintray.com/openrndr/openrndr/' }
    maven { url 'https://dl.bintray.com/ylegall/maven-libs' }
    maven { url 'https://jitpack.io' }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}

task run(dependsOn: 'classes', type: JavaExec) {
    main = 'org.ygl.openrndr.demos.' + System.getProperty("class") + 'Kt'
    classpath = sourceSets.main.runtimeClasspath
}

switch (org.gradle.internal.os.OperatingSystem.current()) {
    case org.gradle.internal.os.OperatingSystem.WINDOWS:
        project.ext.openrndrOS = "windows"
        break
    case org.gradle.internal.os.OperatingSystem.LINUX:
        project.ext.openrndrOS = "linux-x64"
        break
    case org.gradle.internal.os.OperatingSystem.MAC_OS:
        project.ext.openrndrOS = "macos"
        break
}

dependencies {
    runtimeOnly "org.openrndr:openrndr-gl3:$openrndrVersion"
    runtimeOnly "org.openrndr:openrndr-gl3-natives-$openrndrOS:$openrndrVersion"

    implementation "org.openrndr:openrndr-core:$openrndrVersion"
    implementation "org.openrndr:openrndr-svg:$openrndrVersion"
    implementation "org.openrndr:openrndr-animatable:$openrndrVersion"
    implementation "org.openrndr:openrndr-extensions:$openrndrVersion"
    implementation "org.openrndr:openrndr-filter:$openrndrVersion"

    implementation "org.openrndr:openrndr-ffmpeg:$openrndrVersion"
    runtimeOnly "org.openrndr:openrndr-ffmpeg-natives-$openrndrOS:$openrndrVersion"

    implementation "org.openrndr.extra:orx-no-clear:$orxVersion"
    implementation "org.openrndr.extra:orx-fx:$orxVersion"
    implementation "org.openrndr.extra:orx-camera:$orxVersion"
    implementation "org.openrndr.extra:orx-compositor:$orxVersion"
    implementation "org.openrndr.extra:orx-shade-styles:$orxVersion"

    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'

    // replace the following with functional slf4j library when using slf4j
    implementation 'org.slf4j:slf4j-nop:1.7.25'

    implementation 'org.ygl:openrndr-utils:0.0.6'
    implementation 'org.ygl:kx-animation:0.0.2'
}
